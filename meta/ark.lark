// Ark-1: The Kinetic Grammar (Infix/Algol)
// Matches meta/factorial.ark

start: statement+

?statement: assignment
          | flow_stmt
          | expression

assignment: IDENTIFIER ":=" expression
flow_stmt: if_stmt | while_stmt

if_stmt: "if" expression "{" block "}" ["else" "{" block "}"]
while_stmt: "while" expression "{" block "}"

block: statement*

?expression: comparison

?comparison: sum
           | comparison ">" sum -> gt
           | comparison "<" sum -> lt
           | comparison "==" sum -> eq

?sum: product
    | sum "+" product -> add
    | sum "-" product -> sub

?product: atom
        | product "*" atom -> mul
        | product "/" atom -> div

?atom: NUMBER -> number
     | STRING -> string
     | IDENTIFIER -> var
     | function_call
     | "(" expression ")"

function_call: IDENTIFIER "(" [expr_list] ")"
expr_list: expression ("," expression)*

// Tokens
IDENTIFIER: /[a-zA-Z_][a-zA-Z0-9_\.]*/
NUMBER: /-?\d+/
STRING: /".*?"/

%import common.WS
%import common.SH_COMMENT
%ignore WS
%ignore SH_COMMENT
